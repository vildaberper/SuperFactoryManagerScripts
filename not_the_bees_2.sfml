name "NotTheBees2"

every 1 ticks do
    input fe:: from power south side
    output fe:: to crafter_honey
    output fe:: to powered_centrifuge
    output fe:: to heated_centrifuge
    forget
end

every 20 ticks do
-- bottles
    input honey_bottle from hive slots 2-10
    output to crafter_honey
    forget
    input from crafter_honey slots 36-39
    output glass_bottle to glass_bottles
    output except glass_bottle to controller
    forget
    input glass_bottle from glass_bottles
    output retain 1 to each hive slots 0
    forget
end

every 20+1 ticks do
-- vanilla combs
    input honeycomb, honeycomb_block from hive slots 2-10
    output to controller
    forget
-- combs
    input from hive slots 2-10
    output productivebees:configurable_honeycomb, productivebees:configurable_comb to controller_combs
    output except productivebees:configurable_honeycomb, productivebees:configurable_comb to controller
    forget
end

every 20+2 ticks do
-- centrifuges
    input from controller_combs
    output productivebees:configurable_honeycomb to powered_centrifuge slots 1
    output productivebees:configurable_comb to heated_centrifuge slots 1
    forget
    input from powered_centrifuge, heated_centrifuge output to controller forget
    input fluid:: from powered_centrifuge, heated_centrifuge output fluid:: to controller forget
end

every 20+3 ticks do
-- genes
  input productivebees:gene from hive slots 2-10
  output to gene_indexer
  forget
end
